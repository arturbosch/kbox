buildscript {
    ext {
        kbox_version = '0.1.1-SNAPSHOT'
        atrium_version = '0.6.0'
        kotlin_version = '1.2.21'
        mockito_kotlin_version = '1.5.0'
        dokka_version = '0.9.14'

        srcKotlin = 'src/main/kotlin'

        //spek.gradle
        jacoco_tool_version = '0.8.0'
        spek_version = '1.1.5'
        junit_version = '1.1.0'

        //publish.gradle
        bintray_version = '1.7.3'
        publish_artifactId = 'kbox'
        publish_version = kbox_version
        publish_description = 'A utility library for Kotlin'
        publish_vcsUrl = 'https://github.com/robstoll/kbox.git'
    }

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'kotlin'
apply from: './gradle/scripts/spek.gradle'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    runtime "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "ch.tutteli:atrium-cc-en_UK-robstoll:$atrium_version"
    testCompile "ch.tutteli:atrium-spec:$atrium_version"
    testCompile ("com.nhaarman:mockito-kotlin:$mockito_kotlin_version"){
        exclude group: 'org.jetbrains.kotlin'
    }
}

def projectsWithSources = project
configure(projectsWithSources) {
    apply plugin: 'org.jetbrains.dokka'

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/kdoc"
        linkMapping {
            dir = "$srcKotlin"
            url = "https://github.com/robstoll/${project.name}/tree/master/${project.name}/$srcKotlin"
            suffix = '#L'
        }

        sourceDirs = files()
    }

    task sourceJar(type: Jar) {
        from project.sourceSets.main.allSource
        classifier 'sources'
    }

    task javadocJar(type: Jar, dependsOn: dokka) {
        from dokka.outputDirectory
        classifier = 'javadoc'
    }

    afterEvaluate {
        it.ext.publish_artifacts = [sourceJar, javadocJar]
        apply from: './gradle/scripts/publish.gradle'
    }
}


